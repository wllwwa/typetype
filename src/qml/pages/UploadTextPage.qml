import QtQuick 2.15
import QtQuick.Controls 2.15 as QQC
import QtQuick.Layouts 1.15
import QtQuick.Window 2.15
import RinUI

FluentPage {
    id: uploadPage
    property bool active: false  // 由 NavigationView 注入
    title: qsTr("上传文本")
    contentSpacing: 4

    onActiveChanged: {
        if (active) {
            clearForm();
        }
    }

    property bool toLocal: true
    property bool uploading: false
    property string filePath: ""  // 导入的文件路径
    property int maxContentLength: 10000  // 手动输入字数上限

    function clearForm() {
        titleField.text = "";
        contentArea.text = "";
        filePath = "";
        localCheckBox.checked = true;
        toLocal = true;
    }

    function showInfo(severity, title, text) {
        if (Window.window && Window.window.appNotificationManager) {
            // 验证类错误停留较久，成功/提示类自动关闭
            var duration = severity === Severity.Error ? 5000 : 2500
            Window.window.appNotificationManager.show(severity, title, text, duration)
        }
    }

    // 标题
    SettingCard {
        Layout.fillWidth: true
        title: qsTr("标题")
        description: qsTr("文本的显示名称")
        icon.name: "ic_fluent_text_font_20_regular"

        TextField {
            id: titleField
            Layout.preferredWidth: 260
            placeholderText: qsTr("请输入文本标题")
        }
    }

    // 内容
    SettingCard {
        Layout.fillWidth: true
        title: qsTr("内容")
        description: qsTr("文件导入无大小限制，手动输入限 " + maxContentLength + " 字")
        icon.name: "ic_fluent_document_text_20_regular"

        Button {
            text: qsTr("从文件导入")
            icon.name: "ic_fluent_folder_open_20_regular"
            onClicked: {
                if (appBridge) appBridge.openTextFileDialog();
            }
        }
    }

    // 内容显示区
    ColumnLayout {
        Layout.fillWidth: true
        spacing: 4

        // 文件导入提示
        Text {
            visible: uploadPage.filePath !== ""
            typography: Typography.Caption
            color: "#666"
            text: qsTr("已导入文件（仅显示前 20 行预览，上传时直接传输文件）")
        }

        // 文件预览（只读）
        Frame {
            visible: uploadPage.filePath !== ""
            Layout.fillWidth: true
            Layout.preferredHeight: 200
            radius: 6
            hoverable: false

            QQC.ScrollView {
                anchors.fill: parent

                TextArea {
                    id: previewArea
                    readOnly: true
                    wrapMode: TextArea.Wrap
                    text: ""
                    font.pixelSize: 14
                }
            }
        }

        // 手动输入区（无文件时显示）
        ColumnLayout {
            visible: uploadPage.filePath === ""
            Layout.fillWidth: true
            spacing: 4

            Frame {
                Layout.fillWidth: true
                Layout.preferredHeight: 200
                radius: 6
                hoverable: false

                QQC.ScrollView {
                    anchors.fill: parent

                    TextArea {
                        id: contentArea
                        placeholderText: qsTr("请输入文本内容")
                        wrapMode: TextArea.Wrap
                        font.pixelSize: 14
                        onTextChanged: {
                            if (text.length > maxContentLength) {
                                var cursorPos = cursorPosition;
                                text = text.substring(0, maxContentLength);
                                cursorPosition = Math.min(cursorPos, maxContentLength);
                                showInfo(Severity.Warning, qsTr("字数限制"), qsTr("已截断至 " + maxContentLength + " 字，请使用文件导入大文本"));
                            }
                        }
                    }
                }
            }

            // 字数统计
            RowLayout {
                Layout.fillWidth: true

                Text {
                    typography: Typography.Caption
                    color: contentArea.text.length >= maxContentLength ? Theme.currentTheme.colors.systemCriticalColor : Theme.currentTheme.colors.textSecondaryColor
                    text: contentArea.text.length + "/" + maxContentLength
                }

                Item { Layout.fillWidth: true }

                Text {
                    visible: contentArea.text.length >= maxContentLength * 0.9
                    typography: Typography.Caption
                    color: Theme.currentTheme.colors.systemCriticalColor
                    text: contentArea.text.length >= maxContentLength
                          ? qsTr("已达字数上限，请精简内容或使用文件导入")
                          : qsTr("即将达到字数上限")
                }
            }
        }
    }

    // 上传目标
    Text {
        Layout.topMargin: 8
        typography: Typography.Subtitle
        text: qsTr("上传目标")
    }

    SettingCard {
        Layout.fillWidth: true
        title: qsTr("本地文本库")
        description: qsTr("保存到本地，可离线使用")
        icon.name: "ic_fluent_folder_20_regular"

        CheckBox {
            id: localCheckBox
            checked: true
            onCheckedChanged: uploadPage.toLocal = checked
        }
    }

    // 上传进度
    ProgressBar {
        Layout.fillWidth: true
        visible: uploadPage.uploading
        indeterminate: true
    }

    // 按钮区
    Item {
        Layout.fillHeight: true
    }

    RowLayout {
        Layout.fillWidth: true
        spacing: 8

        Item { Layout.fillWidth: true }

        Button {
            text: qsTr("取消")
            onClicked: clearForm()
        }

        Button {
            id: uploadBtn
            text: qsTr("上传")
            highlighted: true
            enabled: !uploadPage.uploading
            onClicked: {
                var title = titleField.text.trim();
                if (!title) {
                    showInfo(Severity.Error, qsTr("验证失败"), qsTr("请输入标题"));
                    return;
                }
                if (!uploadPage.toLocal) {
                    showInfo(Severity.Error, qsTr("验证失败"), qsTr("请选择本地文本库"));
                    return;
                }

                // 检查内容：文件路径或手动输入
                var hasFile = uploadPage.filePath !== "";
                var hasContent = contentArea.text.trim() !== "";
                if (!hasFile && !hasContent) {
                    showInfo(Severity.Error, qsTr("验证失败"), qsTr("请选择文件或输入内容"));
                    return;
                }

                // 手动输入时检查字数
                if (!hasFile && contentArea.text.length > maxContentLength) {
                    showInfo(Severity.Error, qsTr("验证失败"), qsTr("内容超出字数限制，请精简内容或使用文件导入"));
                    return;
                }

                uploadPage.uploading = true;
                if (appBridge) {
                    if (hasFile) {
                        appBridge.uploadTextFromFile(title, uploadPage.filePath, "custom", uploadPage.toLocal, false);
                    } else {
                        appBridge.uploadText(title, contentArea.text.trim(), "custom", uploadPage.toLocal, false);
                    }
                }
            }
        }
    }

    Connections {
        target: appBridge
        enabled: appBridge !== null

        function onUploadResult(success, message, textId) {
            uploadPage.uploading = false;
            if (success) {
                clearForm();
                showInfo(Severity.Success, qsTr("上传成功"), message || qsTr("文本已成功上传"));
            } else {
                showInfo(Severity.Error, qsTr("上传失败"), message);
            }
        }
        function onTextFileLoaded(preview) {
            if (preview) {
                previewArea.text = preview;
                showInfo(Severity.Info, qsTr("文件已导入"), qsTr("已显示前 20 行预览，上传时直接传输文件"));
            } else {
                showInfo(Severity.Error, qsTr("导入失败"), qsTr("无法读取文件内容"));
            }
        }
        function onTextFilePathLoaded(path) {
            uploadPage.filePath = path;
        }
    }
}
